plugins {
	id 'com.android.library'
	id 'kotlin-android'
}

android {
	namespace 'com.serenegiant.janus'
	compileSdkVersion versionCompiler

	compileOptions {
		sourceCompatibility javaSourceCompatibility
		targetCompatibility javaTargetCompatibility
	}

	defaultConfig {
		minSdkVersion 16
		targetSdkVersion versionTarget
		versionCode versionCodeNum
		versionName janusVersionNameString

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	kotlinOptions.jvmTarget = "17"
}

dependencies {
	api fileTree(dir: 'libs', include: ['*.jar'])

	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.ext:junit:1.3.0'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

	implementation "androidx.appcompat:appcompat:${androidXVersion}"
//	implementation "androidx.legacy:legacy-support-v4:${androidXLegacyV4}"

	implementation "com.google.code.gson:gson:${gsonVersion}"
	implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
	testImplementation "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"
	implementation "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
	implementation "com.squareup.retrofit2:retrofit:${retrofit2Version}"
	implementation "com.squareup.retrofit2:converter-gson:${retrofit2Version}"
	implementation "com.squareup.retrofit2:adapter-rxjava3:${retrofit2Version}"

	implementation ("io.socket:socket.io-client:${socketIOClientJavaVersion}") {
	  // excluding org.json which is provided by Android
		exclude group: 'org.json', module: 'json'
	}

	implementation "io.reactivex.rxjava3:rxjava:${rxJava3Version}"
	implementation "io.reactivex.rxjava3:rxkotlin:${rxKotlin3Version}"
	implementation "io.reactivex.rxjava3:rxandroid:${rxJava3AndroidVersion}"
	implementation "com.github.akarnokd:rxjava3-retrofit-adapter:${rxJava3AdaptorVersion}"

	implementation "com.tinder.scarlet:scarlet:${scarletVersion}"
	implementation "com.tinder.scarlet:websocket-okhttp:${scarletVersion}"
	implementation "com.tinder.scarlet:message-adapter-gson:${scarletVersion}"
	implementation "com.tinder.scarlet:stream-adapter-rxjava2:${scarletVersion}"

	implementation "com.serenegiant:common:${commonLibVersion}"

	implementation project(':libwebrtc')
}

def repo = new File(rootDir, "repository")

apply plugin: 'maven-publish'

afterEvaluate {
	publishing {
		repositories {
			maven {
				url = "file://${repo.absolutePath}"
			}
		}
		publications {
			release(MavenPublication) {
				groupId = 'com.serenegiant'
				artifactId = 'janus'
				version = janusVersionNameString
				from components.release
				pom {
					inceptionYear = '2018'
					licenses {
						license {
							name = 'The WebRTC Software License'
							url = 'https://webrtc.org/support/license'
							distribution = 'repo'
						}
					}
					developers {
						developer {
							id = 'saki4510t'
							name = 't_saki'
							email = 't_saki@serenegiant.com'
						}
					}
				}
			}
		}
	}
}
